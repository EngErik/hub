apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'artifactory'
apply plugin: 'application'

sourceCompatibility = 1.7
targetCompatibility = 1.7

project.ext.jenkinsBuildNumber = "$System.env.BUILD_NUMBER"
project.ext.jenkinsJobName = "$System.env.JOB_NAME"

group = 'com.flightstats'

version = '0.1.0'       //TODO: Read from file
def snapshotVersion = version + "-SNAPSHOT"

project.ext.isDevBuild = !project.ext.properties.containsKey("buildConfig") || !"release".equals(project.ext.buildConfig)

if (project.ext.isDevBuild) {
    def buildNumber = "dev";
    if (System.env.BUILD_NUMBER != null) {
        buildNumber = System.env.BUILD_NUMBER
    }
    version = snapshotVersion + "-" + buildNumber
}
println "Builing version: ${version}"

buildDir = 'build'

mainClassName = "com.flightstats.datahub.app.DataHubMain"

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://repo.jfrog.org/artifactory/gradle-plugins' }
    }

    dependencies {  //Required to pick up the artifactory plugin
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '2.0.16')
    }
}

repositories {
    repositories {
        maven {
            url 'http://artifactory.office/artifactory/plugins-dev'
        }
        maven {
            url 'http://artifactory.office/artifactory/plugins-release'
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
        ivy {
            url "http://artifactory.office/artifactory/simple/services-dev"
            layout "pattern", {
                artifact "[organization]/[module]/[revision]/[artifact]-[revision](-[suffix]).[ext]"
            }
        }
    }
}

configurations {
    codeCoverage
    codeCoverageAnt
    publish
    publishDev
    integrationTests
}

task integrationTar(type: Tar, dependsOn: 'distTar') {
    baseName = "datahub-integration"
    compression = Compression.GZIP
    from "${projectDir}"
    include "bin/*.*"
    include "conf/**"
    include "src/test/integration/*.*"
    include "settings.gradle"
}

artifacts {
    publishDev jar
    publishDev distTar
    publishDev integrationTar

    publish jar
    publish distTar
}

artifactory {
    publish {
        contextUrl = 'http://artifactory.office/artifactory'   //The base Artifactory URL for the publisher
        repository {
            repoKey = 'services-release'//The Artifactory repository key to publish to
            if (project.ext.isDevBuild) {
                repoKey = 'services-dev'
            }
            username = 'build'          //The publisher user name
            password = 'P4ssw0rd'       //The publisher password
            ivy {
                ivyLayout = '[organization]/[module]/[revision]/ivy-[revision].xml'
                artifactLayout = '[organization]/[module]/[revision]/[artifact]-[revision](-[suffix]).[ext]'
                mavenCompatible = false //Convert any dots in an [organization] layout value to path separators, similar to Maven's groupId-to-path conversion. True if not specified
            }
        }
        defaults {
            def cc = 'publish'
            if (project.ext.isDevBuild) {
                cc = 'publishDev'
            }
            publishConfigs(cc)
            publishBuildInfo = true
            publishArtifacts = true
            publishPom = false
        }
    }
}

task setupIntegration << {
    println "Setting up the integration environment"
    def integrationTarball = configurations.integrationTests.files.find {
        it.name.startsWith('datahub-integration')
    }

    println "Extracting " + integrationTarball.name
    copy {
        from tarTree(resources.gzip(integrationTarball))
        into projectDir
    }

    def distributionsDir = new File("${buildDir}/distributions")
    distributionsDir.mkdirs()
    def snapshotTarball = configurations.integrationTests.files.find {
        !it.name.startsWith('datahub-integration') && it.name.endsWith('tgz')
    }
    copy {
        from snapshotTarball
        into distributionsDir
    }
}

task integrationTests(dependsOn: setupIntegration) << {
    def testdir = "${projectDir}/src/test/integration"
    def reportsdir = "${projectDir}/build/reports/integration/"
    println "Making " + reportsdir
    new File(reportsdir).mkdirs()
    tasks.deployDev.execute()
    runExternal("jasmine-node --junitreport --output ${reportsdir} ${testdir}")
}

dependencies {
    compile group: 'org.hectorclient', name: 'hector-core', version: '1.+'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.0.0.v20130308'
    compile group: 'org.eclipse.jetty', name: 'jetty-webapp', version: '9.0.0.v20130308'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.0.0.v20130308'
    compile group: 'org.eclipse.jetty', name: 'jetty-util', version: '9.0.0.v20130308'
    compile group: 'org.eclipse.jetty', name: 'jetty-http', version: '9.0.0.v20130308'
    compile group: 'org.eclipse.jetty', name: 'jetty-io', version: '9.0.0.v20130308'
    compile group: 'org.eclipse.jetty', name: 'jetty-security', version: '9.0.0.v20130308'
    compile group: 'org.eclipse.jetty.websocket', name: 'websocket-server', version: '9.0.0.v20130308'
    compile group: 'org.eclipse.jetty.websocket', name: 'websocket-common', version: '9.0.0.v20130308'
    compile group: 'com.sun.jersey', name: 'jersey-core', version: '1.17'
    compile group: 'com.sun.jersey', name: 'jersey-json', version: '1.17'
    compile group: 'com.sun.jersey', name: 'jersey-servlet', version: '1.17'
    compile group: 'com.sun.jersey', name: 'jersey-server', version: '1.17'
    compile group: 'com.sun.jersey.contribs', name: 'jersey-guice', version: '1.17'
    compile group: 'org.codehaus.jackson', name: 'jackson-core-asl', version: '1.9.5'
    compile group: 'org.codehaus.jackson', name: 'jackson-mapper-asl', version: '1.9.5'
    compile group: 'org.codehaus.jackson', name: 'jackson-jaxrs', version: '1.9.5'
    compile group: 'org.codehaus.jackson', name: 'jackson-xc', version: '1.9.5'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
    compile group: 'javax.ws.rs', name: 'jsr311-api', version: '1.1.1'
    compile group: 'asm', name: 'asm', version: '3.1'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.7'
    compile group: 'org.glassfish', name: 'javax.servlet', version: '3.0'
    compile group: 'com.google.inject', name: 'guice', version: '3.0'
    compile group: 'com.google.inject.extensions', name: 'guice-servlet', version: '3.0'
    compile group: 'com.google.guava', name: 'guava', version: '13.0'
    compile group: 'joda-time', name: 'joda-time', version: '2.1'
    compile group: "org.hamcrest", name: "hamcrest-core", version: "1.3"
    compile group: "org.slf4j", name: "slf4j-log4j12", version: "1.7.2"
    compile group: "com.hazelcast", name: "hazelcast", version: "2.5"

    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile group: "org.mockito", name: "mockito-all", version: "1.9.5"

    codeCoverage 'org.jacoco:org.jacoco.agent:0.5.10.201208310627:runtime@jar'
    codeCoverageAnt 'org.jacoco:org.jacoco.ant:0.5.10.201208310627'

    integrationTests group: 'com.flightstats', name: 'datahub', version: snapshotVersion + '+', transitive: false, configuration: 'publishDev'
}

test {
    systemProperties = System.properties
    jvmArgs "-javaagent:${configurations.codeCoverage.asPath}=destfile=${project.buildDir.path}/coverage-results/jacoco.exec,sessionid=HSServ,append=false",
            '-Djacoco=true',
            '-Xms128m',
            '-Xmx512m',
            '-XX:MaxPermSize=128m'
}

// From: http://www.zorched.net/2012/09/13/java-7-code-coverage-with-gradle-and-jacoco/
task generateCoverageReport << {
    ant {
        taskdef(name: 'jacocoreport', classname: 'org.jacoco.ant.ReportTask', classpath: configurations.codeCoverageAnt.asPath)

        mkdir dir: "build/reports/coverage"

        jacocoreport {
            executiondata {
                fileset(dir: "build/coverage-results") {
                    include name: 'jacoco.exec'
                }
            }
            structure(name: project.name) {
                classfiles {
                    fileset(dir: "build/classes/main") {
                    }
                }
                sourcefiles(encoding: 'UTF-8') {
                    fileset dir: "src/main/java"
                }
            }

            xml destfile: "build/reports/coverage/jacoco.xml"
            html destdir: "build/reports/coverage"
        }
    }
}

distTar {
    compression = Compression.GZIP
}

def runExternal(commandline) {
    def proc = commandline.execute()
    proc.in.eachLine { line -> println line }
    proc.err.eachLine { line -> println 'ERROR: ' + line }
    proc.waitFor()
    if (proc.exitValue()) {
        System.exit(proc.exitValue())
    }
}

task deployDev << {
    description = "Deploys the distribution to the development environment"
    runExternal("${projectDir}/bin/restart-dev-vpc.sh")
    runExternal("${projectDir}/bin/deploy-to-dev.sh datahub-01.cloud-east.dev")
}

startScripts {
    ext.jvmOpts = "-Xmx256m"
    doLast {
        unixScript.text = unixScript.text.replace('DEFAULT_JVM_OPTS=""', "DEFAULT_JVM_OPTS=\"$ext.jvmOpts\"")
    }
}